import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group = 'page.nafuchoco'
version = '3.6.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

shadowJar {
    archiveFileName.set(rootProject.name + '.' + archiveExtension.get())
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}
task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}
javadoc {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url = 'https://maven.enginehub.org/repo/'
    }
    maven {
        url = 'https://repo.dmulloy2.net/nexus/repository/public/'
    }
    maven {
        url = 'http://maven.playpro.com/'
    }
    maven {
        url = 'https://maven.aura-dev.team/repository/auradev-releases/'
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:20.1.0'

    compileOnly 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.2'
    compileOnly 'net.coreprotect:coreprotect:19.4'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.5.1'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.apache.commons:commons-lang3:3.11'

    implementation 'team.aura_dev.lib.slf4j-plugin.spigot:slf4j-plugin-spigot:1.2.0.39:1.7.25'
    implementation 'com.zaxxer:HikariCP:4.0.1'
    implementation 'mysql:mysql-connector-java:8.0.23'
    implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.1'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}
